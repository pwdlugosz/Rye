CONNECT
{
	TEMP TO 'C:\Users\pwdlu_000\Documents\Data\TempDB';
};

PRINT
	FIN.BS_CALL(114.92, 115D, 0.0025D, 0D, 0.24, 4D / 52D),
	FIN.BS_CALL_VOL(0.47, 114.92, 125D, 0.0025D, 0D, 4D / 52D);

DECLARE
{
	STOCK AS DOUBLE = 5D;
	STRIKE AS DOUBLE = 5D;
	RATE AS DOUBLE = 0D;
	DIV AS DOUBLE = 0D;
	VOL AS DOUBLE = 0.05;
	TIME AS DOUBLE = 1D / 52D;
	SCENARIO_ID AS INT = 0;
	I AS INT = 0;
	J AS INT = 0;
};

CREATE TABLE TEMP.CALL_SCENARIOS
{
	SCENARIO AS INT,
	STOCK_AMT AS DOUBLE,
	STRIKE_AMT AS DOUBLE,
	RATE AS DOUBLE,
	YIELD AS DOUBLE,
	SIGMA AS DOUBLE,
	TIME AS DOUBLE,

	CALL_PRICE AS DOUBLE,
	CALL_DELTA AS DOUBLE,
	CALL_GAMMA AS DOUBLE,
	CALL_THETA AS DOUBLE,
	CALL_VEGA AS DOUBLE,
	CALL_RHO AS DOUBLE,
	CALL_PSI AS DOUBLE

};

FOR STOCK = 10D TO 50D BY 10D
DO
{

	FOR STRIKE = 10D TO 50D BY 10D
	DO
	{
		
		FOR RATE = 0D TO 0.05 BY 0.01
		DO
		{

			FOR DIV = 0D TO 0.05 BY 0.025
			DO
			{

				FOR VOL = 0.1D TO 1D BY 0.1
				DO
				{

					FOR TIME = 1D / 52D TO 13D / 52D BY 1D / 52D
					DO
					{

						SCENARIO_ID++;

						APPEND
						{

							TEMP.CALL_SCENARIOS;
							RETAIN
								SCENARIO_ID,
								STOCK,
								STRIKE,
								RATE,
								DIV,
								VOL,
								TIME,

								FIN.BS_CALL(STOCK, STRIKE, RATE, DIV, VOL, TIME),
								FIN.BS_CALL_DELTA(STOCK, STRIKE, RATE, DIV, VOL, TIME),
								FIN.BS_CALL_GAMMA(STOCK, STRIKE, RATE, DIV, VOL, TIME),
								FIN.BS_CALL_THETA(STOCK, STRIKE, RATE, DIV, VOL, TIME),
								FIN.BS_CALL_VEGA(STOCK, STRIKE, RATE, DIV, VOL, TIME),
								FIN.BS_CALL_RHO(STOCK, STRIKE, RATE, DIV, VOL, TIME),
								FIN.BS_CALL_PSI(STOCK, STRIKE, RATE, DIV, VOL, TIME)
							;

						};

					};

				};

			};

		};

	};

};

FILE.EXPORT
{
	PATH = 'C:\Users\pwdlu_000\Documents\Finance\Call_Scenarios.csv';
	DATA = TABLE TEMP.CALL_SCENARIOS;
	DELIM = ',';
};
