

CONNECT
{
	TEMP TO 'C:\Users\pwdlu_000\Documents\Data\TempDB';
};


DECLARE
{
	S AS DOUBLE = 100D;
	D AS DOUBLE = 0D;
	K AS DOUBLE = 100D;
	R AS DOUBLE = 0.0025;
	VOL AS DOUBLE = 0.25;
	T AS DOUBLE = 0.038; // 0.058 = 3 WEEKS, 0.077 = 4 WEEKS
	SCENARIO AS INT = 0;

	SCENARIO_ID AS INT;
	P_VAL AS DOUBLE;
	PV_STOCK_AMT AS DOUBLE;
	PV_STRIKE_AMT AS DOUBLE;
	PV_CALL_AMT AS DOUBLE;
	PV_PUT_AMT AS DOUBLE;
	FV_STOCK_AMT AS DOUBLE;
	FV_STRIKE_AMT AS DOUBLE;
	FV_CALL_AMT AS DOUBLE;
	FV_PUT_AMT AS DOUBLE;

};

CREATE TABLE GLOBAL.CALL_OPTION_SIM
{
	SCENARIO_ID AS INT,
	P_VAL AS DOUBLE,
	PV_STOCK_AMT AS DOUBLE,
	PV_STRIKE_AMT AS DOUBLE,
	PV_CALL_AMT AS DOUBLE,
	PV_PUT_AMT AS DOUBLE,
	FV_STOCK_AMT AS DOUBLE,
	FV_STRIKE_AMT AS DOUBLE,
	FV_CALL_AMT AS DOUBLE,
	FV_PUT_AMT AS DOUBLE,
	CALL_IND AS DOUBLE,
	PUT_IND AS DOUBLE
};

FOR SCENARIO_ID = 1 TO 10000
DO
{

	P_VAL = RAND(20161105);
	
	FV_STOCK_AMT = EXP((D - 0.50 * VOL ^ 2D) * T + VOL * (T ^ 0.5) * FIN.NINV(P_VAL)) * S;
	FV_STRIKE_AMT = K;
	FV_CALL_AMT = SMAX(FV_STOCK_AMT - FV_STRIKE_AMT, 0D);
	FV_PUT_AMT = SMAX(FV_STRIKE_AMT - FV_STOCK_AMT, 0D);

	PV_STOCK_AMT = FV_STOCK_AMT * EXP(-T * R);
	PV_STRIKE_AMT = FV_STRIKE_AMT * EXP(-T * R);
	PV_CALL_AMT = FV_CALL_AMT * EXP(-T * R);
	PV_PUT_AMT = FV_PUT_AMT * EXP(-T * R);

	APPEND
	{

		GLOBAL.CALL_OPTION_SIM;
		RETAIN
			SCENARIO_ID,
			P_VAL,
			PV_STOCK_AMT,
			PV_STRIKE_AMT,
			PV_CALL_AMT,
			PV_PUT_AMT,
			FV_STOCK_AMT,
			FV_STRIKE_AMT,
			FV_CALL_AMT,
			FV_PUT_AMT,
			PV_CALL_AMT > 0D ? 1D : 0D AS CALL_IND,
			PV_PUT_AMT > 0D ? 1D : 0D AS PUT_IND
			;

	};

};

TABLIX.EXPORT
{
	DATA = TABLE GLOBAL.CALL_OPTION_SIM;
	PATH = 'C:\Users\pwdlu_000\Documents\Rye Projects\Flat Files\CallOptionSim.csv';
	DELIM = ',';
};

PRINT FIN.BS_CALL(S, K, D, R, VOL, T);

